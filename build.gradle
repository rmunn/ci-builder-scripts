/*
 * Copyright (c) 2017 SIL International
 * Original file copied from https://github.com/sheehan/job-dsl-gradle-example
 * This file is licensed under the Apache License 2.0 (http://www.apache.org/licenses/)
 */

apply plugin: 'groovy'

sourceSets {
	jobs {
		groovy {
			srcDirs 'build/jobs'
			compileClasspath += main.compileClasspath
		}
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}
}

repositories {
	mavenCentral()
	jcenter()
	maven { url 'http://repo.jenkins-ci.org/releases/' }
}

configurations {
	testPlugins {}
}

// Exclude buggy Xalan dependency this way the JRE default TransformerFactory is used
// The xalan pulled in by htmlunit does not properly deal with spaces folder / job names
configurations.all*.exclude group: 'xalan'

dependencies {
	compile 'org.codehaus.groovy:groovy-all:2.4.7'
	compile "org.jenkins-ci.plugins:job-dsl-core:${jobDslVersion}"

	testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
	testCompile 'cglib:cglib-nodep:2.2.2' // used by Spock

	// Jenkins test harness dependencies
	testCompile 'org.jenkins-ci.main:jenkins-test-harness:2.8'
	testCompile "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}"
	testCompile "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}:war-for-test@jar"

	// Job DSL plugin including plugin dependencies
	testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}"
	testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}@jar"
	testCompile 'org.jenkins-ci.plugins:structs:1.2@jar'
	testCompile 'org.jenkins-ci.plugins:cloudbees-folder:6.0.3@jar'

	// plugins to install in test instance (and minimal version)
	testPlugins 'org.jenkins-ci.plugins:ghprb:1.36.0'
	testPlugins 'com.coravy.hudson.plugins.github:github:1.26.2'
	testPlugins 'org.jenkins-ci.plugins:cloudbees-folder:6.0.3'
	testPlugins 'org.jenkins-ci.plugins:nested-view:1.14'
	testPlugins 'org.jenkins-ci.plugins:categorized-view:1.10'
	testPlugins 'org.jenkins-ci.plugins:buildtriggerbadge:2.8.1'
	testPlugins 'org.jenkins-ci.plugins:build-pipeline-plugin:1.5.6'
	testPlugins 'org.jenkins-ci.plugins.workflow:workflow-aggregator:2.5'
	testPlugins 'org.jenkins-ci.plugins.workflow:workflow-api:2.3'
	testPlugins 'org.jenkins-ci.plugins.workflow:workflow-cps:2.19'
	testPlugins 'org.jenkins-ci.plugins.workflow:workflow-job:2.10'
	testPlugins 'org.jenkins-ci.plugins.workflow:workflow-multibranch:2.14'
	testPlugins 'org.jenkins-ci.plugins.workflow:workflow-scm-step:2.2'
	testPlugins 'org.jenkins-ci.plugins.workflow:workflow-step-api:2.7'
	testPlugins 'org.jenkins-ci.plugins:branch-api:2.0.7'
	testPlugins 'org.jenkins-ci.plugins:scm-api:2.0.2'
	testPlugins 'org.jenkins-ci.plugins:script-security:1.23'
	testPlugins 'org.jenkins-ci.plugins:build-timeout:1.18'
	testPlugins 'org.jenkins-ci.plugins:PrioritySorter:3.5.0'
	testPlugins 'org.jenkins-ci.plugins:ansicolor:0.5.0'
	testPlugins 'org.jenkins-ci.plugins:timestamper:1.8.8'
	testPlugins 'org.jenkins-ci.plugins:claim:2.9'
	testPlugins 'org.jenkins-ci.plugins:flexible-publish:0.15.2'
	testPlugins 'org.jenkins-ci.plugins:any-buildstep:0.1'
	testPlugins 'org.jenkins-ci.plugins:parameterized-trigger:2.33'
	testPlugins 'org.jenkins-ci.plugins:mercurial:1.59'
	testPlugins 'com.sonyericsson.hudson.plugins.gerrit:gerrit-trigger:2.23.0'
	testPlugins 'org.jenkins-ci.plugins:throttle-concurrents:1.9.0'
	testPlugins 'org.jenkins-ci.plugins:multiple-scms:0.6'
	testPlugins 'org.jenkins-ci.plugins:extra-columns:1.18'
	testPlugins 'org.jenkins-ci.plugins:postbuildscript:0.17'
	testPlugins 'org.jenkins-ci.plugins:xvfb:1.1.3'
	testPlugins 'org.jenkins-ci.plugins:msbuild:1.27'
	testPlugins 'org.jenkins-ci.plugins:job-node-stalker:1.0.5'
	testPlugins 'org.jenkins-ci.plugins:junit:1.20'
	testPlugins 'com.flowdock.jenkins:jenkins-flowdock-plugin:1.1.8'
	testPlugins 'org.jenkins-ci.plugins:copyartifact:1.38.1'
	testPlugins 'org.jenkins-ci.plugins:subversion:2.7.2'
	testPlugins 'org.jenkins-ci.plugins:credentials:2.1.13'
	testPlugins 'org.jenkins-ci.plugins.icon-shim:icon-shim:2.0.3'
	testPlugins 'org.jenkins-ci.plugins:docker-commons:1.6'
	testPlugins 'org.jenkins-ci.plugins:token-macro:2.1'
	testPlugins 'org.jenkins-ci.plugins:git:3.2.0'
	testPlugins 'org.jenkins-ci.main:maven-plugin:2.15.1'

	// won't be needed once we get rid of internal jenkins
	testPlugins 'org.jenkins-ci.plugins:build-publisher:1.21'

	// for the RestApiScriptRunner
	compile('org.codehaus.groovy.modules.http-builder:http-builder:0.7.2') {
		exclude(module: 'groovy')
	}

	// for the RestApiScriptRunner
	compile('org.apache.ant:ant:1.9.7')
}

task resolveTestPlugins(type: Copy) {
	from configurations.testPlugins
	into new File(sourceSets.test.output.resourcesDir, 'test-dependencies')
	include '*.hpi'
	include '*.jpi'
	def mapping = [:]

	doFirst {
		configurations.testPlugins.resolvedConfiguration.resolvedArtifacts.each {
			mapping[it.file.name] = "${it.name}.${it.extension}"
		}
	}
	rename { mapping[it] }

	doLast {
		List<String> baseNames = source*.name.collect { mapping[it] }.collect { it[0..it.lastIndexOf('.') - 1] }
		new File(destinationDir, 'index').setText(baseNames.join('\n'), 'UTF-8')
	}
}

// Allow including of common scripts
// To include a file add a line '//#include path/to/file.ext'
// File location is relative to the source file
task preprocess(type: Copy) {
	from 'groovy'
	include '*.groovy'
	into 'build/jobs'
	filter { String line ->
		def includeToken = '//#include '
		if (line.trim().startsWith(includeToken)) {
			def includeFile = line.trim().substring(includeToken.length())
			return new File("groovy/$includeFile").text
		}
		return line
	}
}

test {
	dependsOn tasks.preprocess, tasks.resolveTestPlugins
	inputs.files sourceSets.jobs.groovy.srcDirs

	// set build directory for Jenkins test harness, JENKINS-26331
	systemProperty 'buildDirectory', project.buildDir.absolutePath
}

task rest(dependsOn: 'classes', type: JavaExec) {
	main = 'com.dslexample.rest.RestApiScriptRunner'
	classpath = sourceSets.main.runtimeClasspath
	systemProperties System.getProperties()
}

task wrapper(type: Wrapper) {
	gradleVersion = '3.4.1'
}
