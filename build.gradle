/*
 * Copyright (c) 2017-2020 SIL International
 * Original file copied from https://github.com/sheehan/job-dsl-gradle-example
 * This file is licensed under the Apache License 2.0 (http://www.apache.org/licenses/)
 */

apply plugin: 'groovy'

ext {
	jobDslVersion = '1.77'
	jenkinsVersion = '2.222.3'
}

sourceSets {
	jobs {
		groovy {
			srcDirs 'build/jobs'
			compileClasspath += main.compileClasspath
		}
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}
}

repositories {
	jcenter()
	mavenCentral()
	maven { url 'https://repo.jenkins-ci.org/releases/' }
	maven { url 'https://repo.spring.io/plugins-release' }
	maven { url 'https://maven.twttr.com' }
	maven { url 'https://repo.eclipse.org/content/groups/releases'}
}

configurations {
	testPlugins {}

	// see JENKINS-45512
	// Exclude buggy Xalan dependency this way the JRE default TransformerFactory is used
	// The xalan pulled in by htmlunit does not properly deal with spaces folder / job names
	testImplementation {
		exclude group: 'xalan'
		exclude group: 'xerces'
	}
}

dependencies {
	implementation 'org.codehaus.groovy:groovy-all:2.5.0'
	implementation "org.jenkins-ci.plugins:job-dsl-core:${jobDslVersion}"
	implementation 'org.kohsuke:github-api:1.106'

	testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'

	// Jenkins test harness dependencies
	testImplementation 'org.jenkins-ci.main:jenkins-test-harness:2.60'
	testImplementation("org.jenkins-ci.main:jenkins-war:${jenkinsVersion}") {
		exclude group: 'org.jenkins-ci.ui', module: 'bootstrap' // https://github.com/sheehan/job-dsl-gradle-example/issues/87
	}

	// Job DSL plugin including plugin dependencies
	testImplementation "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}"
	testImplementation "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}@jar"
	testImplementation 'org.jenkins-ci.plugins:structs:1.20@jar'

	/*
		plugins to install in test instance
		generate list with this Groovy script in the Jenkins Script Console:

			Jenkins.instance.pluginManager.plugins
				.findAll { !(it.shortName in ['job-dsl', 'structs']) }
				.collect { "testPlugins '${it.manifest.mainAttributes.getValue("Group-Id")}:${it.shortName}:${it.version}'" }
				.sort()
				.each { println it }
	*/

	testPlugins 'com.cloudbees.jenkins.plugins:custom-tools-plugin:0.7'
	testPlugins 'com.coravy.hudson.plugins.github:github:1.29.4'
	testPlugins 'com.sonyericsson.hudson.plugins.gerrit:gerrit-trigger:2.30.0'
	testPlugins 'com.sonyericsson.jenkins.plugins.bfa:build-failure-analyzer:1.23.0'
	testPlugins 'io.jenkins.blueocean:blueocean-bitbucket-pipeline:1.18.1'
	testPlugins 'io.jenkins.blueocean:blueocean-commons:1.18.1'
	testPlugins 'io.jenkins.blueocean:blueocean-config:1.18.1'
	testPlugins 'io.jenkins.blueocean:blueocean-core-js:1.18.1'
	testPlugins 'io.jenkins.blueocean:blueocean-dashboard:1.18.1'
	testPlugins 'io.jenkins.blueocean:blueocean-events:1.18.1'
	testPlugins 'io.jenkins.blueocean:blueocean-executor-info:1.18.1'
	testPlugins 'io.jenkins.blueocean:blueocean-git-pipeline:1.18.1'
	testPlugins 'io.jenkins.blueocean:blueocean-github-pipeline:1.18.1'
	testPlugins 'io.jenkins.blueocean:blueocean-i18n:1.18.1'
	testPlugins 'io.jenkins.blueocean:blueocean-jira:1.18.1'
	testPlugins 'io.jenkins.blueocean:blueocean-jwt:1.18.1'
	testPlugins 'io.jenkins.blueocean:blueocean-personalization:1.18.1'
	testPlugins 'io.jenkins.blueocean:blueocean-pipeline-api-impl:1.18.1'
	testPlugins 'io.jenkins.blueocean:blueocean-pipeline-editor:1.18.1'
	testPlugins 'io.jenkins.blueocean:blueocean-pipeline-scm-api:1.18.1'
	testPlugins 'io.jenkins.blueocean:blueocean-rest-impl:1.18.1'
	testPlugins 'io.jenkins.blueocean:blueocean-rest:1.18.1'
	testPlugins 'io.jenkins.blueocean:blueocean-web:1.18.1'
	testPlugins 'io.jenkins.blueocean:blueocean:1.18.1'
	testPlugins 'io.jenkins.blueocean:jenkins-design-language:1.18.1'
	testPlugins 'org.6wind.jenkins:lockable-resources:2.5'
	testPlugins 'org.jenkins-ci.main:maven-plugin:3.4'
	testPlugins 'org.jenkins-ci.plugins:ansicolor:0.6.2'
	testPlugins 'org.jenkins-ci.plugins:antisamy-markup-formatter:1.6'
	testPlugins 'org.jenkins-ci.plugins:apache-httpcomponents-client-4-api:4.5.5-3.0'
	testPlugins 'org.jenkins-ci.plugins:authentication-tokens:1.3'
	testPlugins 'org.jenkins-ci.plugins:authorize-project:1.3.0'
	testPlugins 'org.jenkins-ci.plugins:basic-branch-build-strategies:1.3.2'
	testPlugins 'org.jenkins-ci.plugins:blueocean-autofavorite:1.2.4'
	testPlugins 'org.jenkins-ci.plugins:blueocean-display-url:2.3.0'
	testPlugins 'org.jenkins-ci.plugins:bouncycastle-api:2.17'
	testPlugins 'org.jenkins-ci.plugins:branch-api:2.5.4'
	testPlugins 'org.jenkins-ci.plugins:build-name-setter:2.0.1'
	testPlugins 'org.jenkins-ci.plugins:build-timeout:1.19'
	testPlugins 'org.jenkins-ci.plugins:build-token-root:1.5'
	testPlugins 'org.jenkins-ci.plugins:buildtriggerbadge:2.10'
	testPlugins 'org.jenkins-ci.plugins:built-on-column:1.1'
	testPlugins 'org.jenkins-ci.plugins:categorized-view:1.10'
	testPlugins 'org.jenkins-ci.plugins:cloudbees-bitbucket-branch-source:2.4.6'
	testPlugins 'org.jenkins-ci.plugins:cloudbees-folder:6.9'
	testPlugins 'org.jenkins-ci.plugins:command-launcher:1.3'
	testPlugins 'org.jenkins-ci.plugins:conditional-buildstep:1.3.6'
	testPlugins 'org.jenkins-ci.plugins:config-file-provider:3.6.2'
	testPlugins 'org.jenkins-ci.plugins:console-column-plugin:1.5'
	testPlugins 'org.jenkins-ci.plugins:copyartifact:1.42.1'
	testPlugins 'org.jenkins-ci.plugins:credentials-binding:1.20'
	testPlugins 'org.jenkins-ci.plugins:credentials:2.3.0'
	testPlugins 'org.jenkins-ci.plugins:dashboard-view:2.11'
	testPlugins 'org.jenkins-ci.plugins:disable-github-multibranch-status:1.1'
	testPlugins 'org.jenkins-ci.plugins:display-url-api:2.3.2'
	testPlugins 'org.jenkins-ci.plugins:docker-commons:1.15'
	testPlugins 'org.jenkins-ci.plugins:docker-workflow:1.19'
	testPlugins 'org.jenkins-ci.plugins:dtkit-api:2.1.1-1'
	testPlugins 'org.jenkins-ci.plugins:durable-task:1.30'
	testPlugins 'org.jenkins-ci.plugins:embeddable-build-status:2.0.2'
	testPlugins 'org.jenkins-ci.plugins:envinject-api:1.6'
	testPlugins 'org.jenkins-ci.plugins:envinject:2.2.0'
	testPlugins 'org.jenkins-ci.plugins:extended-choice-parameter:0.78'
	testPlugins 'org.jenkins-ci.plugins:extra-columns:1.20'
	testPlugins 'org.jenkins-ci.plugins:flexible-publish:0.15.2'
	testPlugins 'org.jenkins-ci.plugins:ghprb:1.42.0'
	testPlugins 'org.jenkins-ci.plugins:git-client:2.8.2'
	testPlugins 'org.jenkins-ci.plugins:git-server:1.8'
	testPlugins 'org.jenkins-ci.plugins:git:3.12.0'
	testPlugins 'org.jenkins-ci.plugins:github-api:1.106'
	testPlugins 'org.jenkins-ci.plugins:github-branch-source:2.6.0'
	testPlugins 'org.jenkins-ci.plugins:github-organization-folder:1.6'
	testPlugins 'org.jenkins-ci.plugins:gradle:1.33'
	testPlugins 'org.jenkins-ci.plugins:groovy:2.2'
	testPlugins 'org.jenkins-ci.plugins:handy-uri-templates-2-api:2.1.7-1.0'
	testPlugins 'org.jenkins-ci.plugins:htmlpublisher:1.18'
	testPlugins 'org.jenkins-ci.plugins:jackson2-api:2.9.9.1'
	testPlugins 'org.jenkins-ci.plugins:javadoc:1.5'
	testPlugins 'org.jenkins-ci.plugins:jdk-tool:1.3'
	testPlugins 'org.jenkins-ci.plugins:jira:3.0.9'
	testPlugins 'org.jenkins-ci.plugins:jquery:1.12.4-1'
	testPlugins 'org.jenkins-ci.plugins:jsch:0.1.55.1'
	testPlugins 'org.jenkins-ci.plugins:junit:1.28'
	testPlugins 'org.jenkins-ci.plugins:mailer:1.24'
	testPlugins 'org.jenkins-ci.plugins:matrix-auth:2.4.2'
	testPlugins 'org.jenkins-ci.plugins:matrix-project:1.14'
	testPlugins 'org.jenkins-ci.plugins:mercurial:2.8'
	testPlugins 'org.jenkins-ci.plugins:modernstatus:1.2'
	testPlugins 'org.jenkins-ci.plugins:msbuild:1.29'
	testPlugins 'org.jenkins-ci.plugins:multiple-scms:0.6'
	testPlugins 'org.jenkins-ci.plugins:nested-view:1.17'
	testPlugins 'org.jenkins-ci.plugins:nodejs:1.3.3'
	testPlugins 'org.jenkins-ci.plugins:nodenamecolumn:1.2'
	testPlugins 'org.jenkins-ci.plugins:nunit:0.25'
	testPlugins 'org.jenkins-ci.plugins:parameterized-trigger:2.35.2'
	testPlugins 'org.jenkins-ci.plugins:pipeline-build-step:2.9'
	testPlugins 'org.jenkins-ci.plugins:pipeline-github-lib:1.0'
	testPlugins 'org.jenkins-ci.plugins:pipeline-graph-analysis:1.10'
	testPlugins 'org.jenkins-ci.plugins:pipeline-input-step:2.11'
	testPlugins 'org.jenkins-ci.plugins:pipeline-milestone-step:1.3.1'
	testPlugins 'org.jenkins-ci.plugins:pipeline-stage-step:2.3'
	testPlugins 'org.jenkins-ci.plugins:plain-credentials:1.5'
	testPlugins 'org.jenkins-ci.plugins:promoted-builds:3.3'
	testPlugins 'org.jenkins-ci.plugins:pubsub-light:1.13'
	testPlugins 'org.jenkins-ci.plugins:radiatorviewplugin:1.29'
	testPlugins 'org.jenkins-ci.plugins:release:2.10.2'
	testPlugins 'org.jenkins-ci.plugins:role-strategy:2.13'
	testPlugins 'org.jenkins-ci.plugins:run-condition:1.2'
	testPlugins 'org.jenkins-ci.plugins:saferestart:0.3'
	testPlugins 'org.jenkins-ci.plugins:scm-api:2.6.3'
	testPlugins 'org.jenkins-ci.plugins:script-security:1.62'
	testPlugins 'org.jenkins-ci.plugins:slack:2.32'
	testPlugins 'org.jenkins-ci.plugins:sse-gateway:1.17'
	testPlugins 'org.jenkins-ci.plugins:ssh-credentials:1.17.1'
	testPlugins 'org.jenkins-ci.plugins:ssh-slaves:1.30.1'
	testPlugins 'org.jenkins-ci.plugins:throttle-concurrents:2.0.1'
	testPlugins 'org.jenkins-ci.plugins:timestamper:1.10'
	testPlugins 'org.jenkins-ci.plugins:token-macro:2.8'
	testPlugins 'org.jenkins-ci.plugins:translation:1.16'
	testPlugins 'org.jenkins-ci.plugins:trilead-api:1.0.3'
	testPlugins 'org.jenkins-ci.plugins:variant:1.3'
	testPlugins 'org.jenkins-ci.plugins:versioncolumn:2.1'
	testPlugins 'org.jenkins-ci.plugins:windows-slaves:1.4'
	testPlugins 'org.jenkins-ci.plugins:xunit:2.3.5'
	testPlugins 'org.jenkins-ci.plugins:xvfb:1.1.3'
	testPlugins 'org.jenkins-ci.plugins.pipeline-stage-view:pipeline-rest-api:2.12'
	testPlugins 'org.jenkins-ci.plugins.pipeline-stage-view:pipeline-stage-view:2.12'
	testPlugins 'org.jenkins-ci.plugins.workflow:workflow-aggregator:2.6'
	testPlugins 'org.jenkins-ci.plugins.workflow:workflow-api:2.36'
	testPlugins 'org.jenkins-ci.plugins.workflow:workflow-basic-steps:2.18'
	testPlugins 'org.jenkins-ci.plugins.workflow:workflow-cps-global-lib:2.15'
	testPlugins 'org.jenkins-ci.plugins.workflow:workflow-cps:2.73'
	testPlugins 'org.jenkins-ci.plugins.workflow:workflow-durable-task-step:2.33'
	testPlugins 'org.jenkins-ci.plugins.workflow:workflow-job:2.34'
	testPlugins 'org.jenkins-ci.plugins.workflow:workflow-multibranch:2.21'
	testPlugins 'org.jenkins-ci.plugins.workflow:workflow-scm-step:2.9'
	testPlugins 'org.jenkins-ci.plugins.workflow:workflow-step-api:2.20'
	testPlugins 'org.jenkins-ci.plugins.workflow:workflow-support:3.3'
	testPlugins 'org.jenkins-ci.ui:ace-editor:1.1'
	testPlugins 'org.jenkins-ci.ui:handlebars:1.1.1'
	testPlugins 'org.jenkins-ci.ui:jquery-detached:1.2.1'
	testPlugins 'org.jenkins-ci.ui:momentjs:1.1.1'
	testPlugins 'org.jenkinsci.plugins:pipeline-model-api:1.3.9'
	testPlugins 'org.jenkinsci.plugins:pipeline-model-declarative-agent:1.1.1'
	testPlugins 'org.jenkinsci.plugins:pipeline-model-definition:1.3.9'
	testPlugins 'org.jenkinsci.plugins:pipeline-model-extensions:1.3.9'
	testPlugins 'org.jenkinsci.plugins:pipeline-stage-tags-metadata:1.3.9'
	testPlugins 'org.jvnet.hudson.plugins:favorite:2.3.2'

	// for the RestApiScriptRunner
	implementation('org.codehaus.groovy.modules.http-builder:http-builder:0.7.2') {
		exclude(module: 'groovy')
	}

	// for the RestApiScriptRunner
	implementation('org.apache.ant:ant:1.10.7')
}

task resolveTestPlugins(type: Copy) {
	from configurations.testPlugins
		into new File(sourceSets.test.output.resourcesDir, 'test-dependencies')
		include '*.hpi'
		include '*.jpi'
	def renames = [:]

	doFirst {
		configurations.testPlugins.resolvedConfiguration.resolvedArtifacts.unique(false, {
			a, b -> a.name <=> b.name
		}).each {
			renames[it.file.name] = "${it.name}.${it.extension}"
		}
	}
	rename { renames[it] }

	doLast {
		def mapping = [:]
		configurations.testPlugins.resolvedConfiguration.resolvedArtifacts.each {
			mapping[it.file.name] = "${it.name}.${it.extension}"
		}
		List<String> baseNames = source*.name.collect { mapping[it] }.collect { it[0..it.lastIndexOf('.') - 1] }
		new File(destinationDir, 'index').setText(baseNames.join('\n'), 'UTF-8')
	}
}

// Allow including of common scripts
// To include a file add a line '//#include path/to/file.ext'
// File location is relative to the source file
task preprocess(type: Copy) {
	from 'groovy'
	include '*.groovy'
	into 'build/jobs'
	filter { String line ->
		def includeToken = '//#include '
		if (line.trim().startsWith(includeToken)) {
			def includeFile = line.trim().substring(includeToken.length())
			return new File("groovy/$includeFile").text
		}
		return line
	}
}

test {
	dependsOn tasks.preprocess, tasks.resolveTestPlugins
	inputs.files sourceSets.jobs.groovy.srcDirs

	project.logger.lifecycle("Testing with Jenkins ${jenkinsVersion}, Job DSL ${jobDslVersion}")

	// set build directory for Jenkins test harness, JENKINS-26331
	systemProperty 'buildDirectory', project.buildDir.absolutePath
}

task rest(dependsOn: 'classes', type: JavaExec) {
	main = 'com.dslexample.rest.RestApiScriptRunner'
	classpath = sourceSets.main.runtimeClasspath
	systemProperties System.getProperties()
}
